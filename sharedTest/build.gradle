// The sharedTest module is meant to be included in the app module 'test' and 'androidTest'
// source sets, but instead takes the shape of an Android library to work correctly with Android Studio.

plugins {
    id "com.android.library"
    id "org.jetbrains.kotlin.android"
}

kotlin {
    jvmToolchain 17
}

android {
    namespace = "com.maltaisn.notesshared"
    compileSdk = 36

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 36
    }

    sourceSets {
        // This just add "kotlin" folder to source sets since the default is "java".
        main {
            java.srcDirs += "src/main/kotlin"
        }
    }

    packagingOptions {
        // see https://stackoverflow.com/questions/44342455
        resources.excludes.add("META-INF/*")
    }
}

tasks.named("build") {
    // don't test, don't lint, don't run detekt for build task.
    setDependsOn(getDependsOn().findAll { it != "check" })
}

dependencies {
    implementation project(":app")

    api "junit:junit:$junitVersion"
    api "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"
    api "org.mockito.kotlin:mockito-kotlin:$kotlinMockitoVersion"
    api "androidx.arch.core:core-testing:$androidxArchTestingVersion"
    api "androidx.test:core:$androidxTestCoreVersion"
    api "androidx.test:core-ktx:$androidxTestCoreKtxVersion"
    api "androidx.test.ext:junit:$androidxTestExtVersion"
    api "androidx.test.ext:junit-ktx:$androidxTestExtVersion"
    api "androidx.test:rules:$androidxTestRulesVersion"
}
